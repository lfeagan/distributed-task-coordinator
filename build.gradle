buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:5.+"
        // classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.27.1"
        classpath "com.github.evestera:plugin:0.1.0"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.15"
    }
}

plugins {
    id "edu.sc.seis.version-class" version "1.3.0"
}

allprojects {
    group = 'com.github.lfeagan'
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    // Adds a version qualifier to the end of the version (i.e. SNAPSHOT, RC)
    project.version += rootProject.hasProperty('versionQualifier') ? '-' + rootProject.property('versionQualifier') : ''
}

repositories {
    mavenCentral()
}

ext {
    // Published Projects
    publishedProjects = [
            project(':api'),
            project(':postgresql')
    ]
}

//
// Common configuration for all subprojects
// Borrowed from: https://github.com/h2oai/sparkling-water/blob/master/build.gradle
configure(subprojects) { project ->
    repositories {
        // Should be enabled only in development mode
        if (project.hasProperty('useMavenLocal') || versionQualifier.endsWith("-SNAPSHOT")) {
            mavenLocal()
        }

        mavenCentral()

        // Public Sonatype repository
        maven {
            url "https://oss.sonatype.org/content/repositories/releases/"
        }
    }

    // Publish artifacts
    if (project in publishedProjects) {
        apply from: "$rootDir/gradle/publish.gradle"
    }
}
